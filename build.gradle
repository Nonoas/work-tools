import io.github.fvarrui.javapackager.gradle.PackageTask

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'io.github.fvarrui:javapackager:1.7.5'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

group 'indi.nonoas'
version '1.3.3'

sourceCompatibility = '17'

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}


repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    versions = [
            'jfx': '22'
    ]
}

javafx {
    version = "${versions.jfx}"
    modules = ['javafx.controls', 'javafx.swing']
}


dependencies {
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'org.quartz-scheduler:quartz:2.3.2'
    implementation 'com.melloware:jintellitype:1.4.0'
    implementation 'com.h2database:h2:2.2.220'
    implementation 'com.alibaba.fastjson2:fastjson2:2.0.47'
    implementation 'org.freemarker:freemarker:2.3.30'
    implementation 'org.flywaydb:flyway-core:10.12.0'
    implementation 'cn.hutool:hutool-db:5.8.25'
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    implementation 'io.github.nonoas:jfx-flat-ui:1.0.1'
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation "org.openjfx:javafx-controls:${versions.jfx}"
    implementation "com.kodedu.terminalfx:terminalfx:1.2.0"

}

mainClassName = 'indi.nonoas.worktools.MainKt'

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

tasks.register('packageMyApp', PackageTask) {
    dependsOn clean, jar
    // mandatory
    mainClass = mainClassName
    // optional
    modules = ["java.base", "java.management", "java.net.http", "java.scripting", "java.sql", "java.naming",
               "jdk.jsobject", "jdk.unsupported", "jdk.unsupported.desktop", "jdk.xml.dom"]
    bundleJre = true
    generateInstaller = false
    administratorRequired = false
    winConfig { createZipball = true }

}

